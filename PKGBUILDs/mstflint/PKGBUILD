# Maintainer: James Harvey <jamespharvey20@gmail.com>
# namcap says dependency 'libibmad' is not needed, but without it, configure fails with "cannot find infiniband/mad.h", provided by libibmad, and mstflint-4.0.1/mtcr_ul/mtcr_ib_ofed.c does include "infiniband/mad.h"

pkgname=mstflint
pkgver=4.9.0
_pkgver_subver=3
pkgrel=1
pkgdesc='OpenFabrics Alliance firmware burning application for Mellanox HCA/NIC cards'
arch=('x86_64' 'i686')
url='https://www.openfabrics.org/index.php/overview.html'
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibmad' 'libxml2' 'python' 'curl')
source=("$pkgname::git+https://github.com/Mellanox/mstflint#tag=v${pkgver}-${_pkgver_subver}")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
  CXXFLAGS='-O3 -Wno-cast-function-type -Wno-maybe-uninitialized -Wno-format-truncation -Wno-class-memaccess'\
  CFLAGS='-O3 -Wno-cast-function-type -Wno-maybe-uninitialized -Wno-format-truncation'\
  LDFLAGS='-O3 -flto' ./configure --prefix=/usr \
              --enable-fw-mgr
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
