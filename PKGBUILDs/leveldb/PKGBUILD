# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: feydaykyn <feydaykyn@yahoo.fr>

pkgname=leveldb-nodep
pkgver=1.22
pkgrel=1
pkgdesc="A fast and lightweight key/value database library"
arch=('x86_64')
url="https://github.com/google/leveldb"
license=('BSD')
provides=('leveldb')
conflicts=('leveldb')
makedepends=('cmake')
source=("leveldb-$pkgver.tar.gz::https://github.com/google/leveldb/archive/$pkgver.tar.gz")
sha1sums=('SKIP')

prepare() {
  cd "leveldb-$pkgver"
  # disable tcmalloc the hard way
  sed -i -e '/tcmalloc/d' -e '/snappy/d' CMakeLists.txt
}

build() {
  mkdir "leveldb-$pkgver/build"
  cd "leveldb-$pkgver/build"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=1 \
    ..

  make
}

package() {
  cd "leveldb-$pkgver"
  make -C build DESTDIR="$pkgdir" install
}
